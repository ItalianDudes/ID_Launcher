plugins {
    id 'com.gradleup.shadow' version '8.3.6'
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

// Versioning
def versionNumber = '1.3'
def versionType = 'RELEASE' // RELEASE | BETA | ALPHA | DEV
def versionMarker = versionType.charAt(0)

group = 'it.italiandudes'
version = versionNumber+versionMarker

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

javafx {
    version = "21.0.7"
    modules = [
            'javafx.base',
            'javafx.controls',
            'javafx.graphics',
            'javafx.fxml',
            'javafx.swing',
            'javafx.media'
    ]
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.1'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'commons-codec:commons-codec:1.17.1'
    implementation 'it.italiandudes:idl:1.0.9'
    implementation 'org.json:json:20240303'
}

shadowJar {
    mainClassName = 'it.italiandudes.id_launcher.ID_Launcher'
    manifest {
        attributes "Main-Class" : mainClassName
        attributes "Title" : "ID Launcher"
        attributes "Vendor" : "ItalianDudes"
        attributes "Version" : archiveVersion.get()
        attributes "Version-Number" : archiveVersion.get()
        attributes "Version-Type" : versionType
        attributes "Version-Marker" : versionMarker
        attributes "Timestamp" : new Date().format("dd/MM/yyyy HH:mm:ss.sss")
    }
    archiveFileName = getRootDir().getName()+"-"+archiveVersion.get()+".jar"
}

tasks.register('buildJar') {
    dependsOn shadowJar
    group = "Build"
    description = "Build and assemble ID_Launcher"
}

tasks.register('runClient', Exec) {
    dependsOn shadowJar
    group = "Execution"
    description = "Build, assemble and run ID_Launcher"
    standardInput = System.in
    commandLine "java", "-jar", jar.archiveFile.get()
}